:namespace UCMD
⍝ UCMD Driver for CITA.
⍝ Cita.dyalog in SALT/Spice is the immutable cover that SALT needs to recognize the UCMDSs.
⍝ In order to handle Run/List/Help, those fns falls the fns in this ns which
⍝ uses the API-Fn "_InitUCMDs" to setup the variables _List/_Help which are then
 ⍝ used to create the info for SALT etc.

    ⎕ML←⎕io←1

    ∇ {r}←Run(cmd args);res;⎕ML;l;rc;log
      eis←{1=≡⍵:⊂⍵ ⋄ ⍵}
      :If 3=⎕NC'⎕SE.CITA.API.',cmd        ⍝ if function exists in ⎕SE.CITA...
          args.SwD⍪←'ucmd' 1 ⋄ args.ucmd←1  ⍝ indicate to the fn that it is called via UCMD
          (rc log)←⍎'⎕SE.CITA.API.',cmd,' args' ⍝ execute it...
          :If rc≡⍬
              r←⍬   ⍝ optionally API-fns may return rc as ⍬ which is an indicator to ommit the header (for "simple" this like reports in ]APLVersion)
          :Else
              r←cmd,': ',{⍵=0:'success' ⋄ ⍵=¯1:'warning' ⋄ ('*** '/⍨0≠⍵),'FAILURE (return code=',(⍕⍵),')'},⊃,rc
              r←l r(l←(⌈/(≢r),≢¨log)⍴'─~-'[0 ¯1⍳⊃,rc])
          :EndIf
          r←r,eis log
          r←∊{1<|≡⍵:(↓⍕↑⍵),¨⎕UCS 13 ⋄ ⍵,⎕UCS 13}¨r
      :Else
          ⎕←↑⎕DMX
          r←''
      :EndIf
    ∇

    ∇ r←List;findLine;nr;fn;ns;hd;maxH
      :If ⎕SE.SALTUtils.V18
          r←⎕JSON ⎕SE.CITA.UCMD._List
      :Else
          r←''
      :EndIf
    ∇

    ∇ r←level Help cmd
      r←⎕SE.CITA.UCMD._Help{(⍺[;⍳2]∧.≡⍵)⌿⍺[;3]}cmd level
      :If ∨/⎕SE.CITA.UCMD._Help[;⍳2]∧.≡cmd(level+1)
          r,←(⊂''),(⊂']CITA.',cmd,' -',((level+2)⍴'?'),'    ⍝ for more details')
      :EndIf
    ∇
:endnamespace
